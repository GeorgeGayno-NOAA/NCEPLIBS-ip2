 SUBROUTINE IPMERGE2(NO,NF,M1,L1,F1,M2,L2,F2,MO,LO,FO)
!$$$  SUBPROGRAM DOCUMENTATION BLOCK
!
! SUBPROGRAM:  IPMERGE2   MERGE TWO BITMAPPED FIELDS INTO ONE
!   PRGMMR: IREDELL       ORG: W/NMC23       DATE: 1998-04-08
!
! ABSTRACT: THIS SUBPROGRAM MERGES TWO BITMAPPED FIELDS INTO ONE.
!           WHERE BOTH INPUT FIELDS ARE VALID, THE FIRST FIELD IS TAKEN.
!           WHERE NEITHER INPUT FIELD IS VALID, THE OUTPUT IS INVALID.
!
! PROGRAM HISTORY LOG:
! 1999-04-08  IREDELL
!
! USAGE:    CALL IPMERGE2(NO,NF,M1,L1,F1,M2,L2,F2,MO,LO,FO)
!
!   INPUT ARGUMENT LIST:
!     NO       - INTEGER NUMBER OF POINTS IN EACH FIELD
!     NF       - INTEGER NUMBER OF FIELDS MERGES
!     M1       - INTEGER FIRST DIMENSION OF FIELD 1 ARRAYS
!     L1       - LOGICAL(1) (M1,NF) BITMAP FOR FIELD 1
!     F1       - REAL (M1,NF) DATA FOR FIELD 1
!     M2       - INTEGER FIRST DIMENSION OF FIELD 2 ARRAYS
!     L2       - LOGICAL(1) (M2,NF) BITMAP FOR FIELD 2
!     F2       - REAL (M2,NF) DATA FOR FIELD 2
!     MO       - INTEGER FIRST DIMENSION OF OUTPUT FIELD ARRAYS
!
!   OUTPUT ARGUMENT LIST:
!     LO       - LOGICAL(1) (MO,NF) BITMAP FOR OUTPUT FIELD
!     FO       - REAL (MO,NF) DATA FOR OUTPUT FIELD
!
! ATTRIBUTES:
!   LANGUAGE: FORTRAN 90
!
!$$$
 IMPLICIT NONE
!
 INTEGER,           INTENT(IN   ) :: NO,NF,M1,M2,MO
!
 LOGICAL(1),        INTENT(IN   ) :: L1(M1,NF),L2(M2,NF)
 LOGICAL(1),        INTENT(  OUT) :: LO(MO,NF)
!
 REAL,              INTENT(IN   ) :: F1(M1,NF),F2(M2,NF)
 REAL,              INTENT(  OUT) :: FO(MO,NF)
!
 INTEGER                          :: K, N
! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!  MERGE FIELDS, TAKING FIRST FIELD FIRST
 DO N=1,NF
   DO K=1,NO
     LO(K,N)=L1(K,N).OR.L2(K,N)
     IF(L1(K,N)) THEN
       FO(K,N)=F1(K,N)
     ELSEIF(L2(K,N)) THEN
       FO(K,N)=F2(K,N)
     ELSE
       FO(K,N)=0
     ENDIF
   ENDDO
 ENDDO
! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
 END SUBROUTINE IPMERGE2
