 SUBROUTINE POLATEG0(IPOPT,KGDSI,KGDSO,MI,MO,KM,IBI,LI,GI, &
                     NO,RLAT,RLON,CROT,SROT,IBO,LO,XO,YO,IRET)
!$$$  SUBPROGRAM DOCUMENTATION BLOCK
!
! SUBPROGRAM:  POLATEG0   INTERPOLATE SCALAR FIELD GRADIENTS (BILINEAR)
!   PRGMMR: IREDELL       ORG: W/NMC23       DATE: 96-04-10
!
! ABSTRACT: THIS SUBPROGRAM PERFORMS BILINEAR INTERPOLATION
!           FROM ANY GRID TO ANY GRID FOR SCALAR FIELDS,
!           RETURNING THEIR VECTOR GRADIENTS.
!           NO OPTIONS ARE ALLOWED.
!           ONLY HORIZONTAL INTERPOLATION IS PERFORMED.
!           THE GRIDS ARE DEFINED BY THEIR GRID DESCRIPTION SECTIONS
!           (PASSED IN INTEGER FORM AS DECODED BY SUBPROGRAM W3FI63).
!           THE CURRENT CODE RECOGNIZES THE FOLLOWING PROJECTIONS:
!             (KGDS(1)=000) EQUIDISTANT CYLINDRICAL
!             (KGDS(1)=001) MERCATOR CYLINDRICAL
!             (KGDS(1)=003) LAMBERT CONFORMAL CONICAL
!             (KGDS(1)=004) GAUSSIAN CYLINDRICAL (SPECTRAL NATIVE)
!             (KGDS(1)=005) POLAR STEREOGRAPHIC AZIMUTHAL
!             (KGDS(1)=202) ROTATED EQUIDISTANT CYLINDRICAL (ETA NATIVE)
!           WHERE KGDS COULD BE EITHER INPUT KGDSI OR OUTPUT KGDSO.
!           AS AN ADDED BONUS THE NUMBER OF OUTPUT GRID POINTS
!           AND THEIR LATITUDES AND LONGITUDES ARE ALSO RETURNED.
!           ON THE OTHER HAND, THE OUTPUT CAN BE A SET OF STATION POINTS
!           IF KGDSO(1)<0, IN WHICH CASE THE NUMBER OF POINTS
!           AND THEIR LATITUDES AND LONGITUDES MUST BE INPUT.
!           INPUT BITMAPS WILL BE INTERPOLATED TO OUTPUT BITMAPS.
!           OUTPUT BITMAPS WILL ALSO BE CREATED WHEN THE OUTPUT GRID
!           EXTENDS OUTSIDE OF THE DOMAIN OF THE INPUT GRID.
!           THE OUTPUT FIELD IS SET TO 0 WHERE THE OUTPUT BITMAP IS OFF.
!        
! PROGRAM HISTORY LOG:
!   96-04-10  IREDELL
! 1999-04-08  IREDELL  SPLIT IJKGDS INTO TWO PIECES
! 2001-06-18  IREDELL  FIXED CALL TO IJKGDS1
!
! USAGE:    CALL POLATEG0(IPOPT,KGDSI,KGDSO,MI,MO,KM,IBI,LI,GI,
!    &                    NO,RLAT,RLON,CROT,SROT,IBO,LO,XO,YO,IRET)
!
!   INPUT ARGUMENT LIST:
!     IPOPT    - INTEGER (20) INTERPOLATION OPTIONS (NO OPTIONS)
!     KGDSI    - INTEGER (200) INPUT GDS PARAMETERS AS DECODED BY W3FI63
!     KGDSO    - INTEGER (200) OUTPUT GDS PARAMETERS
!                (KGDSO(1)<0 IMPLIES RANDOM STATION POINTS)
!     MI       - INTEGER SKIP NUMBER BETWEEN INPUT GRID FIELDS IF KM>1
!                OR DIMENSION OF INPUT GRID FIELDS IF KM=1
!     MO       - INTEGER SKIP NUMBER BETWEEN OUTPUT GRID FIELDS IF KM>1
!                OR DIMENSION OF OUTPUT GRID FIELDS IF KM=1
!     KM       - INTEGER NUMBER OF FIELDS TO INTERPOLATE
!     IBI      - INTEGER (KM) INPUT BITMAP FLAGS
!     LI       - LOGICAL*1 (MI,KM) INPUT BITMAPS (IF SOME IBI(K)=1)
!     GI       - REAL (MI,KM) INPUT FIELDS TO INTERPOLATE
!     NO       - INTEGER NUMBER OF OUTPUT POINTS (ONLY IF KGDSO(1)<0)
!     RLAT     - REAL (NO) OUTPUT LATITUDES IN DEGREES (IF KGDSO(1)<0)
!     RLON     - REAL (NO) OUTPUT LONGITUDES IN DEGREES (IF KGDSO(1)<0)
!     CROT     - REAL (NO) VECTOR ROTATION COSINES (IF KGDSO(1)<0)
!     SROT     - REAL (NO) VECTOR ROTATION SINES (IF KGDSO(1)<0)
!                (UGRID=CROT*UEARTH-SROT*VEARTH;
!                 VGRID=SROT*UEARTH+CROT*VEARTH)
!
!   OUTPUT ARGUMENT LIST:
!     NO       - INTEGER NUMBER OF OUTPUT POINTS (ONLY IF KGDSO(1)>=0)
!     RLAT     - REAL (MO) OUTPUT LATITUDES IN DEGREES (IF KGDSO(1)>=0)
!     RLON     - REAL (MO) OUTPUT LONGITUDES IN DEGREES (IF KGDSO(1)>=0)
!     IBO      - INTEGER (KM) OUTPUT BITMAP FLAGS
!     LO       - LOGICAL*1 (MO,KM) OUTPUT BITMAPS (ALWAYS OUTPUT)
!     XO       - REAL (MO,KM) OUTPUT X-GRADIENT FIELDS INTERPOLATED
!     YO       - REAL (MO,KM) OUTPUT Y-GRADIENT FIELDS INTERPOLATED
!     IRET     - INTEGER RETURN CODE
!                0    SUCCESSFUL INTERPOLATION
!                2    UNRECOGNIZED INPUT GRID OR NO GRID OVERLAP
!                3    UNRECOGNIZED OUTPUT GRID
!
! SUBPROGRAMS CALLED:
!   GDSWZD       GRID DESCRIPTION SECTION WIZARD
!   IJKGDS0      SET UP PARAMETERS FOR IJKGDS1
!   (IJKGDS1)    RETURN FIELD POSITION FOR A GIVEN GRID POINT
!   POLFIXS      MAKE MULTIPLE POLE SCALAR VALUES CONSISTENT
!
! REMARKS:  THE GRADIENT COMPUTATIONS ARE NOT ROBUST NEAR THE POLES.
!   IN FACT, NO GRADIENTS ARE COMPUTED POLEWARD OF 89 LATITUDE.
!
! ATTRIBUTES:
!   LANGUAGE: FORTRAN 90
!
!$$$
 IMPLICIT NONE
!
 INTEGER,    INTENT(IN   ) :: IPOPT(20), IBI(KM)
 INTEGER,    INTENT(IN   ) :: KGDSI(200),KGDSO(200)
 INTEGER,    INTENT(IN   ) :: KM, MI, MO
 INTEGER,    INTENT(INOUT) :: NO
 INTEGER,    INTENT(  OUT) :: IBO(KM), IRET
!
 LOGICAL*1,  INTENT(IN   ) :: LI(MI,KM)
 LOGICAL*1,  INTENT(  OUT) :: LO(MO,KM)
!
 REAL,       INTENT(IN   ) :: GI(MI,KM)
 REAL,       INTENT(INOUT) :: CROT(MO),SROT(MO)
 REAL,       INTENT(INOUT) :: RLAT(MO),RLON(MO)
 REAL,       INTENT(  OUT) :: XO(MO,KM),YO(MO,KM)
!
 REAL,       PARAMETER     :: FILL=-9999.
 REAL,       PARAMETER     :: PLAT=89.
 REAL,       PARAMETER     :: RERTH=6.3712E6
!
 INTEGER                   :: I1,J1,I2,J2
 INTEGER                   :: IJKGDS1,IJKGDSA(20),K,N,NV
 INTEGER                   :: N11(MO),N21(MO),N12(MO),N22(MO)
!
 REAL                      :: CLAT(MO),DPR,FACX,FACY
 REAL,       ALLOCATABLE   :: AREA(:),DUM1(:),DUM2(:)
 REAL                      :: WX11(MO),WX21(MO),WX12(MO),WX22(MO)
 REAL                      :: WY11(MO),WY21(MO),WY12(MO),WY22(MO)
 REAL                      :: XPTS(MO),YPTS(MO)
 REAL                      :: XLON(MO),XLAT(MO)
 REAL                      :: YLON(MO),YLAT(MO)
 REAL                      :: XF, YF, XI, YI, XROT, YROT
! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!  COMPUTE NUMBER OF OUTPUT POINTS AND THEIR LATITUDES AND LONGITUDES.
 IRET=0
 IF(KGDSO(1).GE.0) THEN
   ALLOCATE(AREA(MO))
   CALL GDSWZD(KGDSO, 0,MO,FILL,XPTS,YPTS,RLON,RLAT,NO,1,CROT,SROT, &
               0,XLON,XLAT,YLON,YLAT,AREA)
   DEALLOCATE(AREA)
   IF(NO.EQ.0) IRET=3
 ENDIF
! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!  LOCATE INPUT POINTS AND COMPUTE THEIR WEIGHTS
 DPR=180/ACOS(-1.)
 DO N=1,NO
   CLAT(N)=COS(RLAT(N)/DPR)
 ENDDO
 ALLOCATE(DUM1(NO))
 ALLOCATE(DUM2(NO))
 ALLOCATE(AREA(NO))
 CALL GDSWZD(KGDSI,-1,NO,FILL,XPTS,YPTS,RLON,RLAT,NV,0, &
             DUM1,DUM2,1,XLON,XLAT,YLON,YLAT,AREA)
 DEALLOCATE(DUM1,DUM2,AREA)
 IF(IRET.EQ.0.AND.NV.EQ.0) IRET=2
 CALL IJKGDS0(KGDSI,IJKGDSA)
 DO N=1,NO
   XI=XPTS(N)
   YI=YPTS(N)
   IF(XI.NE.FILL.AND.YI.NE.FILL.AND.ABS(RLAT(N)).LE.PLAT) THEN
     I1=XI
     I2=I1+1
     J1=YI
     J2=J1+1
     XF=XI-I1
     YF=YI-J1
     N11(N)=IJKGDS1(I1,J1,IJKGDSA)
     N21(N)=IJKGDS1(I2,J1,IJKGDSA)
     N12(N)=IJKGDS1(I1,J2,IJKGDSA)
     N22(N)=IJKGDS1(I2,J2,IJKGDSA)
     IF(MIN(N11(N),N21(N),N12(N),N22(N)).GT.0) THEN
       FACX=DPR/(RERTH*CLAT(N))
       WX11(N)=(-(1-YF)*XLON(N)-(1-XF)*YLON(N))*FACX
       WX21(N)=((1-YF)*XLON(N)-XF*YLON(N))*FACX
       WX12(N)=(-YF*XLON(N)+(1-XF)*YLON(N))*FACX
       WX22(N)=(YF*XLON(N)+XF*YLON(N))*FACX
       FACY=DPR/RERTH
       WY11(N)=(-(1-YF)*XLAT(N)-(1-XF)*YLAT(N))*FACY
       WY21(N)=((1-YF)*XLAT(N)-XF*YLAT(N))*FACY
       WY12(N)=(-YF*XLAT(N)+(1-XF)*YLAT(N))*FACY
       WY22(N)=(YF*XLAT(N)+XF*YLAT(N))*FACY
     ELSE
       N11(N)=0
       N21(N)=0
       N12(N)=0
       N22(N)=0
     ENDIF
   ELSE
     N11(N)=0
     N21(N)=0
     N12(N)=0
     N22(N)=0
   ENDIF
 ENDDO
! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!  INTERPOLATE WITH OR WITHOUT BITMAPS
 DO K=1,KM
   IBO(K)=0
   DO N=1,NO
     LO(N,K)=N11(N).GT.0.AND.(IBI(K).EQ.0.OR. &
            (LI(N11(N),K).AND.LI(N21(N),K).AND. &
             LI(N12(N),K).AND.LI(N22(N),K)))
     IF(LO(N,K)) THEN
       XO(N,K)=WX11(N)*GI(N11(N),K)+WX21(N)*GI(N21(N),K) &
               +WX12(N)*GI(N12(N),K)+WX22(N)*GI(N22(N),K)
       YO(N,K)=WY11(N)*GI(N11(N),K)+WY21(N)*GI(N21(N),K) &
               +WY12(N)*GI(N12(N),K)+WY22(N)*GI(N22(N),K)
       XROT=CROT(N)*XO(N,K)-SROT(N)*YO(N,K)
       YROT=SROT(N)*XO(N,K)+CROT(N)*YO(N,K)
       XO(N,K)=XROT
       YO(N,K)=YROT
     ELSE
       IBO(K)=1
       XO(N,K)=0.
       YO(N,K)=0.
     ENDIF
   ENDDO
 ENDDO
 IF(KGDSO(1).EQ.0) CALL POLFIXV(NO,MO,KM,RLAT,RLON,IBO,LO,XO,YO)
! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
 END SUBROUTINE POLATEG0
