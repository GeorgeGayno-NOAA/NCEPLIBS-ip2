 FUNCTION GCDIST(RLAT1,RLON1,RLAT2,RLON2)
!$$$  SUBPROGRAM DOCUMENTATION BLOCK
!
! SUBPROGRAM:  GCDIST     COMPUTE GREAT CIRCLE DISTANCE
!   PRGMMR: IREDELL       ORG: W/NMC23       DATE: 96-04-10
!
! ABSTRACT: THIS SUBPROGRAM COMPUTES GREAT CIRCLE DISTANCE
!           BETWEEN TWO POINTS ON THE EARTH.
!
! PROGRAM HISTORY LOG:
!   96-04-10  IREDELL
!
! USAGE:    ...GCDIST(RLAT1,RLON1,RLAT2,RLON2)
!
!   INPUT ARGUMENT LIST:
!     RLAT1    - REAL LATITUDE OF POINT 1 IN DEGREES
!     RLON1    - REAL LONGITUDE OF POINT 1 IN DEGREES
!     RLAT2    - REAL LATITUDE OF POINT 2 IN DEGREES
!     RLON2    - REAL LONGITUDE OF POINT 2 IN DEGREES
!
!   OUTPUT ARGUMENT LIST:
!     GCDIST   - REAL GREAT CIRCLE DISTANCE IN METERS
!
! ATTRIBUTES:
!   LANGUAGE: FORTRAN 90
!
!$$$
 IMPLICIT NONE
!
 REAL                    :: GCDIST
!
 REAL,     INTENT(IN   ) :: RLAT1
 REAL,     INTENT(IN   ) :: RLON1
 REAL,     INTENT(IN   ) :: RLAT2
 REAL,     INTENT(IN   ) :: RLON2
!
 REAL,     PARAMETER     :: RERTH=6.3712E6
 REAL,     PARAMETER     :: PI=3.14159265358979
 REAL,     PARAMETER     :: DPR=180./PI
!
 REAL                    :: CDLON, CRD
 REAL                    :: CLAT1, CLAT2, SLAT1, SLAT2
!
 CLAT1=COS(RLAT1/DPR)
 SLAT1=SIN(RLAT1/DPR)
 CLAT2=COS(RLAT2/DPR)
 SLAT2=SIN(RLAT2/DPR)
 CDLON=COS((RLON1-RLON2)/DPR)
 CRD=SLAT1*SLAT2+CLAT1*CLAT2*CDLON
 GCDIST=RERTH*ACOS(CRD)
! 
 END FUNCTION GCDIST
